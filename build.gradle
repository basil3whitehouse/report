defaultTasks 'clean', 'test'

ext {
    damageControlReportGroupId = 'com.github.damage-control.report'
    damageControlReportVersion = '1.0-SNAPSHOT'
}

subprojects {

    group = damageControlReportGroupId
    version = damageControlReportVersion 

    repositories {
        mavenCentral()
    }
}

project(':html-generator') {
    
    apply plugin: 'groovy'
    apply plugin: 'codenarc'
    apply plugin: 'maven'

    codenarc.configFile = file('../codenarc.groovy')

    dependencies {
        groovy          group: 'org.codehaus.groovy',   name: 'groovy-all',     version: '1.8.6'
        compile         group: 'org.codehaus.groovy',   name: 'groovy-all',     version: '1.8.6'
        compile         group: 'commons-io',            name: 'commons-io',     version: '1.3.2'
        testCompile     group: 'org.spockframework',    name: 'spock-core',     version: '0.6-groovy-1.8'
    }

    install.dependsOn check, test
}

project(':plugins:maven') {
    task install(type: Exec, dependsOn: ':html-generator:install') {
        def ext = System.getProperty('os.name').contains('Win') ? '.bat' : ''
        def mvn = System.getenv('M2_HOME') + '/bin/mvn' + ext

        environment 'JAVA_HOME', System.getenv('JAVA_HOME')
        environment 'M2_HOME', System.getenv('M2_HOME')

        executable mvn
        args 'clean'
        args 'install'
    }
}

project(':end-2-end-test') {
    
    apply plugin: 'groovy'

    dependencies {
        groovy          group: 'org.codehaus.groovy',   name: 'groovy-all',     version: '1.8.6'
        testCompile     group: 'org.spockframework',    name: 'spock-core',     version: '0.6-groovy-1.8'
    }

    test.dependsOn ':plugins:maven:install'
}
